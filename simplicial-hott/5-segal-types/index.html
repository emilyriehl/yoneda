<!DOCTYPE html>
<html class="writer-html5" lang="en" >
<head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <link rel="shortcut icon" href="../../img/favicon.ico" />
    <title>Segal Types - Yoneda</title>
    <link rel="stylesheet" href="../../css/theme.css" />
    <link rel="stylesheet" href="../../css/theme_extra.css" />
        <link href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.7.0/styles/default.min.css" rel="stylesheet" />
    <link rel="apple-touch-icon" sizes="180x180" href="/rzk/apple-touch-icon.png">
    <link rel="icon" type="image/png" sizes="32x32" href="/rzk/favicon-32x32.png">
    <link rel="icon" type="image/png" sizes="16x16" href="/rzk/favicon-16x16.png">
    <link rel="manifest" href="/rzk/site.webmanifest">
<!-- <link rel="stylesheet" href="../../css/highlightjs-11.7.0.min.css" /> -->
    <link rel="stylesheet" href="../../css/highlightjs-copy.css" />
    <script src="../../js/highlight-11.7.0.min.js"></script>
    <script src="../../js/rzk.js"></script>
    <link rel="stylesheet" href="../../css/gruvbox-light.css" />
    <!-- <link rel="stylesheet" href="https://unpkg.com/highlightjs-copy/dist/highlightjs-copy.min.css" /> -->
    <script src="../../js/highlightjs-copy.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.0/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
    <script>
      hljs.addPlugin(new CopyButtonPlugin());
      hljs.highlightAll();
    </script>
    
    
      <script>
        // Current page data
        var mkdocs_page_name = "Segal Types";
        var mkdocs_page_input_path = "simplicial-hott/5-segal-types.md";
        var mkdocs_page_url = null;
      </script>
    
    <script src="../../js/jquery-3.6.0.min.js" defer></script>
    <!--[if lt IE 9]>
      <script src="../../js/html5shiv.min.js"></script>
    <![endif]-->
 
</head>

<body class="wy-body-for-nav" role="document">

  <div class="wy-grid-for-nav">
    <nav data-toggle="wy-nav-shift" class="wy-nav-side stickynav">
    <div class="wy-side-scroll">
      <div class="wy-side-nav-search">
          <a href="../.." class="icon icon-home"> Yoneda
        </a><div role="search">
  <form id ="rtd-search-form" class="wy-form" action="../../search.html" method="get">
      <input type="text" name="q" placeholder="Search docs" title="Type search term here" />
  </form>
</div>
      </div>

      <div class="wy-menu wy-menu-vertical" data-spy="affix" role="navigation" aria-label="Navigation menu">
              <p class="caption"><span class="caption-text">General</span></p>
              <ul>
                  <li class="toctree-l1"><a class="reference internal" href="../..">About</a>
                  </li>
              </ul>
              <p class="caption"><span class="caption-text">HoTT</span></p>
              <ul>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/0-common/">Common</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/1-paths/">Paths</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/2-contractible/">Contractible</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/3-homotopies/">Homotopies</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/4-equivalences/">Equivalences</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/5-sigma/">Sigma types</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../../hott/6-trivial-fibrations/">Trivial fibrations</a>
                  </li>
              </ul>
              <p class="caption"><span class="caption-text">Simplicial HoTT</span></p>
              <ul class="current">
                  <li class="toctree-l1"><a class="reference internal" href="../3-simplicial-type-theory/">Simplicial Type Theory</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../4-extension-types/">Extension Types</a>
                  </li>
                  <li class="toctree-l1 current"><a class="reference internal current" href="./">Segal Types</a>
    <ul class="current">
    <li class="toctree-l2"><a class="reference internal" href="#prerequisites">Prerequisites</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#hom-types">Hom types</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#the-segal-condition">The Segal condition</a>
        <ul>
    <li class="toctree-l3"><a class="reference internal" href="#characterizing-segal-types">Characterizing Segal types</a>
    </li>
        </ul>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#segal-function-and-extension-types">Segal function and extension types</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#identity">Identity</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#associativity">Associativity</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#homotopies">Homotopies</a>
    </li>
    <li class="toctree-l2"><a class="reference internal" href="#anodyne-maps">Anodyne maps</a>
    </li>
    </ul>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../6-2cat-of-segal-types/">2-Category of Segal Types</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../7-discrete/">Discrete Types</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../8-covariant/">Covariantly Functorial Type Families</a>
                  </li>
                  <li class="toctree-l1"><a class="reference internal" href="../9-yoneda/">The Yoneda Lemma</a>
                  </li>
              </ul>
      </div>
    </div>
    </nav>

    <section data-toggle="wy-nav-shift" class="wy-nav-content-wrap">
      <nav class="wy-nav-top" role="navigation" aria-label="Mobile navigation menu">
          <i data-toggle="wy-nav-top" class="fa fa-bars"></i>
          <a href="../..">Yoneda</a>
        
      </nav>
      <div class="wy-nav-content">
        <div class="rst-content"><div role="navigation" aria-label="breadcrumbs navigation">
  <ul class="wy-breadcrumbs">
    <li><a href="../.." class="icon icon-home" alt="Docs"></a> &raquo;</li>
          <li>Simplicial HoTT &raquo;</li>
      <li>Segal Types</li>
    <li class="wy-breadcrumbs-aside">
    </li>
  </ul>
  <hr/>
</div>
          <div role="main" class="document" itemscope="itemscope" itemtype="http://schema.org/Article">
            <div class="section" itemprop="articleBody">
              
                <h1 id="segal-types">Segal Types</h1>
<p>These formalisations correspond to Section 5 of RS17 paper.</p>
<p>This is a literate <code>rzk</code> file:</p>
<pre><code class="language-rzk">#lang rzk-1
</code></pre>
<h2 id="prerequisites">Prerequisites</h2>
<ul>
<li><code>hott/1-paths.rzk</code> - we require basic path algebra</li>
<li><code>hott/2-contractible.rzk</code> - we require the notion of contractible types and the data associated to a proof of contractibility</li>
<li><code>hott/total-space.rzk</code> — we rely on <code>contractible-fibers-projection-equiv</code> and <code>total-space-projection</code> in the proof of Theorem 5.5</li>
<li><code>3-simplicial-type-theory.md</code> — we rely on definitions of simplicies and their subshapes</li>
<li><code>4-extension-types.md</code> — we use the fubini theorem and extension extensionality</li>
</ul>
<h2 id="hom-types">Hom types</h2>
<p>Extension types are used to define the type of arrows between fixed terms:</p>
<pre><code class="language-rzk">-- [RS17, Definition 5.1]
-- The type of arrows in A from x to y
#def hom
  (A : U)   -- A type.
  (x y : A) -- Two points in A.
  : U
  := (t : Δ¹) -&gt; A [    -- (hom A x y) is a 1-simplex (an arrow) in A where
    t === 0_2 |-&gt; x,    -- * the left endpoint is exactly x
    t === 1_2 |-&gt; y     -- * the right endpoint is exactly y
  ]
</code></pre>
<p>Extension types are also used to define the type of commutative triangles:</p>
<pre><code class="language-rzk">-- [RS17, Definition 5.2]
-- the type of commutative triangles in A
#def hom2
  (A : U)           -- A type.
  (x y z : A)       -- Three points in A.
  (f : hom A x y)   -- An arrow in A from x to y.
  (g : hom A y z)   -- An arrow in A from y to z.
  (h : hom A x z)   -- An arrow in A from x to z.
  : U
  := { (t1, t2) : Δ² } -&gt; A [   -- (hom2 A x y z f g h) is a 2-simplex (triangle) in A where
    t2 === 0_2 |-&gt; f t1,        -- * the top edge is exactly f,
    t1 === 1_2 |-&gt; g t2,        -- * the right edge is exactly g, and
    t2 === t1  |-&gt; h t2         -- * the diagonal is exactly h
  ]
</code></pre>
<h2 id="the-segal-condition">The Segal condition</h2>
<p>A type is Segal if every composable pair of arrows has a unique composite.</p>
<pre><code class="language-rzk">-- [RS17, Definition 5.3]
#def isSegal 
  (A : U)         -- A type.
  : U
  := (x : A) -&gt; (y : A) -&gt; (z : A) -&gt; (f : hom A x y) -&gt; (g : hom A y z) -&gt; isContr( ∑ (h : hom A x z), hom2 A x y z f g h)
</code></pre>
<p>Segal types have a composition functor and witnesses to the composition relation:</p>
<pre><code class="language-rzk">-- Composition is written in diagrammatic order to match the order of arguments in isSegal.
#def Segal-comp 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : hom A x z
  := first (first (AisSegal x y z f g))

-- Segal types have composition witnesses
#def Segal-comp-witness 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : hom2 A x y z f g (Segal-comp A AisSegal x y z f g)
  := second (first (AisSegal x y z f g))
</code></pre>
<p>Composition in a Segal type is unique in the following sense. If there is a witness that an arrow h is a composite of f and g, then the specified composite equals h.</p>
<pre><code class="language-rzk">#def Segal-comp-uniqueness 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  (h : hom A x z)               -- An arrow in A from x to z.
  (alpha : hom2 A x y z f g h)  -- A witness that h is a composite of f and g.
  : (Segal-comp A AisSegal x y z f g) = h
  := total-path-to-base-path 
      (hom A x z)
      (\k -&gt; hom2 A x y z f g k)
      (Segal-comp A AisSegal x y z f g, Segal-comp-witness A AisSegal x y z f g)
      (h, alpha)
      (contracting-htpy (∑ (k : hom A x z), hom2 A x y z f g k) (AisSegal x y z f g) (h, alpha))
</code></pre>
<h3 id="characterizing-segal-types">Characterizing Segal types</h3>
<p>Our aim is to prove that a type is Segal if and only if the horn-restriction map, defined below, is an equivalence.</p>
<pre><code class="language-rzk">-- A pair of composable arrows form a horn.
#def horn
  (A : U)                       -- A type.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : Λ -&gt; A
  := \(t, s) -&gt;                 -- \{(t, s) : 2 * 2 | Λ (t, s) }   -&gt;        --  try \{(t, s) : Λ } or with ()
      recOR(
        s === 0_2 |-&gt; f t, 
        t === 1_2 |-&gt; g s
        )

-- The underlying horn of a simplex
#def horn-restriction   
  (A : U)                            -- A type.
  (alpha : Δ² -&gt; A)   -- A commutative triangle.
  : Λ -&gt; A
  :=  \t -&gt; alpha t

-- An alternate definition of Segal types.
#def isSegal'
  (A : U)       -- A type.
  : U
  := isEquiv (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;) (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;) (horn-restriction A) 
</code></pre>
<p>Now we prove this definition is equivalent to the original one.</p>
<pre><code class="language-rzk">-- Here, we prove the equivalence used in [RS17, Theorem 5.5].
-- However, we do this by constructing the equivalence directly,
-- instead of using a composition of equivalences, as it is easier to write down
-- and it computes better (we can use refl for the witnesses of the equivalence).
#def compositions-are-horn-fillings
  (A : U)                       -- A type.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : Eq (∑ (h : hom A x z), hom2 A x y z f g h)
       &lt;{t : 2 * 2 | Δ² t } -&gt; A [ Λ t |-&gt; horn A x y z f g t ]&gt;
  := (\hh -&gt; \{t : 2 * 2 | Δ² t} -&gt; (second hh) t,
      ((\k -&gt; (\(t : 2) -&gt; k (t, t), \{(t, s) : 2 * 2 | Δ² (t, s)} -&gt; k (t, s)), \hh -&gt; refl_{hh}),
       (\k -&gt; (\(t : 2) -&gt; k (t, t), \{(t, s) : 2 * 2 | Δ² (t, s)} -&gt; k (t, s)), \hh -&gt; refl_{hh})))

#def restriction-equiv
  (A : U)                       -- A type.
  : Eq (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;)
      (∑ (k : &lt;{t : 2 * 2 | Λ t} -&gt; A &gt;),
        ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
          hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                 (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
  := (\k -&gt;
      (\{t : 2 * 2 | Λ t} -&gt; k t,
        (\(t : 2) -&gt; k (t, t),
         \{t : 2 * 2 | Δ² t} -&gt; k t)),
      ((\khh -&gt; \{t : 2 * 2 | Δ² t} -&gt; (second (second khh)) t, \k -&gt; refl_{k}),
        (\khh -&gt; \{t : 2 * 2 | Δ² t} -&gt; (second (second khh)) t, \k -&gt; refl_{k})))

-- [RS17, Theorem 5.5], the hard direction:
#def Segal-restriction-equiv 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  : Eq (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;) (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;) -- (horn-restriction A)
  := compose_Eq
        (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;)
        (∑ (k : &lt;{t : 2 * 2 | Λ t} -&gt; A &gt;),
            ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
            hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                    (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
        (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;)
        (restriction-equiv A)
        (total-space-projection
            (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;)
            (\k -&gt; ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
                        hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                            (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h),
        (contractible-fibers-projection-equiv
            (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;)
            (\k -&gt; ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
                        hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                            (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
            (\k -&gt; AisSegal (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                            (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)))))

-- Verify that the mapping in (Segal-restriction-equiv A AisSegal)
-- is exactly (horn-restriction A)
#def Segal-restriction-equiv-test
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  : (first (Segal-restriction-equiv A AisSegal)) = (horn-restriction A)
  := refl


#def isSegal-isSegal' 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  : isSegal' A
  := second (Segal-restriction-equiv A AisSegal)  

#def isSegal'-isSegal 
  (A : U)                       -- A type.
  (AisSegal' : isSegal' A)      -- A proof that A is Segal'.
  : isSegal A
  := \x y z f g -&gt;
      (projection-equiv-contractible-fibers 
        (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;)
        (\k -&gt; ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
                        hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                            (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
        (second (compose_Eq
          (∑ (k : &lt;{t : 2 * 2 | Λ t} -&gt; A &gt;),
            ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
            hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                    (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
          (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;)
          (&lt;{t : 2 * 2 | Λ t} -&gt; A &gt;)
          (sym_Eq
            (&lt;{t : 2 * 2 | Δ² t} -&gt; A &gt;)
            (∑ (k : &lt;{t : 2 * 2 | Λ t} -&gt; A &gt;),
              ∑ (h : hom A (k (0_2, 0_2)) (k (1_2, 1_2))),
              hom2 A (k (0_2, 0_2)) (k (1_2, 0_2)) (k (1_2, 1_2))
                      (\t -&gt; k (t, 0_2)) (\t -&gt; k (1_2, t)) h)
            (restriction-equiv A))
          (horn-restriction A, AisSegal')
        )))
      (horn A x y z f g)  

-- [RS17, Theorem 5.5]; these notions are equivalent, not just logically equivalent, because they are both propositions over A
#def isSegal-iff-isSegal' 
  (A : U)                       -- A type.
  : iff (isSegal A) (isSegal' A)      
  := (isSegal-isSegal' A , isSegal'-isSegal A)
</code></pre>
<h2 id="segal-function-and-extension-types">Segal function and extension types</h2>
<p>Using the new characterization of Segal types, we can show that the type of functions or extensions into a family of Segal types is again a Segal type.</p>
<pre><code class="language-rzk">-- [RS17, Corollary 5.6(i)] : if X is a type and A : X -&gt; U is such that A(x) is a Segal type then (x : X) -&gt; A x is a Segal type
#def Segal-function-types 
  (funext : FunExt)                                 -- This proof uses function extensionality defined in 4-equivalences.rzk
  (X : U)                                           -- A type.
  (A : (_ : X) -&gt; U)                                -- A type family
  (fiberwiseAisSegal : (x : X) -&gt; isSegal' (A x))   -- An assumption that the fibers are Segal types.
  : isSegal' ((x : X) -&gt; A x) 
  := triple_compose_isEquiv
       (&lt;{t : 2 * 2 | Δ² t} -&gt; ((x : X) -&gt; A x) &gt;)
       ((x : X) -&gt; &lt;{t : 2 * 2 | Δ² t} -&gt; A x &gt;) 
       ((x : X) -&gt; &lt;{t : 2 * 2 | Λ t} -&gt; A x &gt;) 
       (&lt;{t : 2 * 2 | Λ t} -&gt; ((x : X) -&gt; A x) &gt;)
        (\g -&gt; \x -&gt; \{t : 2 * 2 | Δ² t} -&gt; g t x) -- first equivalence
            (second (flip-ext-fun
              (2 * 2)
              Δ² (\{t : 2 * 2 | Δ² t} -&gt; BOT)
              X
              (\{t : 2 * 2 | Δ² t} -&gt; A)
              (\{t : 2 * 2 | BOT} -&gt; recBOT)))
        (\h -&gt; \x -&gt; \{t : 2 * 2 | Λ t} -&gt; h x t) -- second equivalence
          (second (fibered-equiv-function-equiv 
              funext 
              X 
              (\x -&gt; &lt;{t : 2 * 2 | Δ² t} -&gt; A x &gt;) 
              (\x -&gt; &lt;{t : 2 * 2 | Λ t} -&gt; A x &gt;) 
              (\x -&gt; (horn-restriction (A x) , fiberwiseAisSegal x))))
        (\h -&gt; \{t : 2 * 2 | Λ t} -&gt; \x -&gt; (h x) t) -- third equivalence
          (second(flip-ext-fun-inv
            (2 * 2)
            Λ (\{t : 2 * 2 | Λ t} -&gt; BOT)
            X
            (\{t : 2 * 2 | Λ t} -&gt; A)
            (\{t : 2 * 2 | BOT} -&gt; recBOT)))

-- [RS17, Corollary 5.6(ii)] : if X is a shape and A : X -&gt; U is such that A(x) is a Segal type then (x : X) -&gt; A x is a Segal type
#def Segal-extension-types 
  (extext : ExtExt)                                             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (I : CUBE)                                                    -- A cube.
  (psi : (s : I) -&gt; TOPE)                                       -- A tope.
  (A : &lt;{s : I | psi s} -&gt; U &gt;)                                 -- An extension type.
  (fiberwiseAisSegal : &lt;{s : I | psi s} -&gt; isSegal' (A s) &gt;)    -- An assumption that the fibers are Segal types.
  : isSegal' (&lt;{s : I | psi s} -&gt; A s &gt;) 
  := triple_compose_isEquiv
        (&lt;{t : 2 * 2 | Δ² t} -&gt; &lt;{s : I | psi s} -&gt; A s &gt; &gt;) 
        (&lt;{s : I | psi s} -&gt; &lt;{t : 2 * 2 | Δ² t} -&gt; A s &gt; &gt;)
        (&lt;{s : I | psi s} -&gt; &lt;{t : 2 * 2 | Λ t} -&gt; A s &gt; &gt;)
        (&lt;{t : 2 * 2 | Λ t} -&gt; &lt;{s : I | psi s} -&gt; A s &gt; &gt;)
        (\g -&gt; \{s : I | psi s} -&gt; \{t : 2 * 2 | Δ² t} -&gt; g t s)  -- first equivalence
            (second(fubini
              (2 * 2)
              I 
              Δ²
              (\{t : 2 * 2 | Δ² t} -&gt; BOT)
              psi
              (\{s : I | psi s} -&gt; BOT)
              (\{t : 2 * 2 | Δ² t} -&gt; \{s : I | psi s} -&gt; A s)
              (\{u : (2 * 2) * I | BOT} -&gt; recBOT)))
        (\h -&gt; \{s : I | psi s} -&gt; \{t : 2 * 2 | Λ t} -&gt; h s t) -- second equivalence
          (second (fibered-equiv-extension-equiv extext I psi  
            (\{s : I | psi s} -&gt; &lt;{t : 2 * 2 | Δ² t} -&gt; A s &gt;)
            (\{s : I | psi s} -&gt; &lt;{t : 2 * 2 | Λ t} -&gt; A s &gt;)
            (\{s : I | psi s} -&gt; (horn-restriction (A s), fiberwiseAisSegal s))     ))
        (\h -&gt; \{t : 2 * 2 | Λ t} -&gt; \{s : I | psi s} -&gt; (h s) t) -- third equivalence
          (second(fubini
            I 
            (2 * 2)
            psi
            (\{s : I | psi s} -&gt; BOT)
            Λ
            (\{t : 2 * 2 | Λ t} -&gt; BOT)
            (\{s : I | psi s} -&gt; \{t : 2 * 2 | Λ t} -&gt; A s)
            (\{u : I * (2 * 2) | BOT} -&gt; recBOT)))        

-- The type of arrows in a type.
#def arr        -- A type
  (A : U) 
  : U
  := (t : Δ¹) -&gt; A

-- A special case of [RS17, Corollary 5.6(ii)], using is-Segal'.
#def Segal'-arrow-types 
  (extext : ExtExt)                         -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                                   -- A type.
  (AisSegal : isSegal' A)                   -- A proof that A isSegal'.
  : isSegal' (arr A)
  := Segal-extension-types
        extext
        2
        Δ¹
        (\{t : 2 | Δ¹ t} -&gt; A)
        (\{t : 2 | Δ¹ t} -&gt; AisSegal)  

-- A special case of [RS17, Corollary 5.6(ii)], using is-Segal.
#def Segal-arrow-types 
  (extext : ExtExt)                         -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                                   -- A type.
  (AisSegal : isSegal A)                    -- A proof that A is Segal.
  : isSegal (arr A)
  := isSegal'-isSegal (arr A)
      (Segal-extension-types
        extext
        2
        Δ¹
        (\{t : 2 | Δ¹ t} -&gt; A)
        (\{t : 2 | Δ¹ t} -&gt; (isSegal-isSegal' A AisSegal)))
</code></pre>
<h2 id="identity">Identity</h2>
<p>All types have identity arrows and witnesses to the identity composition law. </p>
<pre><code class="language-rzk">-- [RS17, Definition 5.7]
-- all types have identity arrows
#def id-arr
  (A : U)               -- A type.
  (x : A)               -- A point in A.
  : hom A x x
  := \{t : 2 | Δ¹ t} -&gt; x 

-- [RS17, Proposition 5.8a]
-- the right unit law for identity
#def comp-id-witness 
  (A : U)                   -- A type.
  (x y : A)                 -- Two points in A.
  (f : hom A x y)           -- An arrow from x to y in A.
  : hom2 A x y y f (id-arr A y) f
  := \{(t, s) : 2 * 2 | Δ² (t, s)} -&gt; f t

-- [RS17, Proposition 5.8b]
-- the left unit law for identity
#def id-comp-witness 
  (A : U)                   -- A type.
  (x y : A)                 -- Two points in A.
  (f : hom A x y)           -- An arrow from x to y in A.
  : hom2 A x x y (id-arr A x) f f
  := \{(t, s) : 2 * 2 | Δ² (t, s)} -&gt; f s
</code></pre>
<p>In a Segal type, where composition is unique, it follows that composition with an identity arrow recovers the original arrow. 
Thus, an identity axiom was not needed in the definition of Segal types.</p>
<pre><code class="language-rzk">-- If A is Segal then the right unit law holds
#def Segal-comp-id
  (A : U)                   -- A type.
  (AisSegal : isSegal A)    -- A proof that A is Segal.
  (x y : A)                 -- Two points in A.
  (f : hom A x y)           -- An arrow from x to y in A.
  : (Segal-comp A AisSegal x y y f (id-arr A y)) =_{hom A x y} f
  := Segal-comp-uniqueness
      A
      AisSegal
      x y y
      f
      (id-arr A y)
      f
      (comp-id-witness A x y f)

-- If A is Segal then the left unit law holds
#def Segal-id-comp
  (A : U)                   -- A type.
  (AisSegal : isSegal A)    -- A proof that A is Segal.
  (x y : A)                 -- Two points in A.
  (f : hom A x y)           -- An arrow from x to y in A.
  : (Segal-comp A AisSegal x x y (id-arr A x) f) =_{hom A x y} f
  := Segal-comp-uniqueness
      A
      AisSegal
      x x y
      (id-arr A x)
      f
      f
      (id-comp-witness A x y f)
</code></pre>
<h2 id="associativity">Associativity</h2>
<p>We now prove that composition in a Segal type is associative, by using the fact that the type of arrows in a Segal type is itself a Segal type.</p>
<pre><code class="language-rzk">#def unfolding-square 
  (A : U)                         -- A type.
  (triangle : Δ² -&gt; A)            -- A triangle in A.
  : Δ¹×Δ¹ -&gt; A                    -- A square in A, defined by gluing two copies of the triangle along the common diagonal edge.
  := \(t, s) -&gt; 
    recOR(t &lt;= s |-&gt; triangle (s , t), 
        s &lt;= t |-&gt; triangle (t , s))

-- for use in the proof of associativity
#def Segal-comp-witness-square 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : Δ¹×Δ¹ -&gt; A 
  := unfolding-square A (Segal-comp-witness A AisSegal x y z f g)

-- the Segal-comp-witness-square as an arrow in the arrow type
#def Segal-arr-in-arr 
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.
  (x y z : A)                   -- Three points in A.
  (f : hom A x y)               -- An arrow in A from x to y.
  (g : hom A y z)               -- An arrow in A from y to z.
  : hom (arr A) f g
  := \t -&gt; \s -&gt; (Segal-comp-witness-square A AisSegal x y z f g) (t, s)

#def Segal-associativity-witness 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : hom2 (arr A) f g h (Segal-arr-in-arr A AisSegal w x y f g) (Segal-arr-in-arr A AisSegal x y z g h)
        (Segal-comp (arr A) (Segal-arrow-types extext A AisSegal) 
          f g h 
          (Segal-arr-in-arr A AisSegal w x y f g) (Segal-arr-in-arr A AisSegal x y z g h))
  := (Segal-comp-witness (arr A) (Segal-arrow-types extext A AisSegal) 
        f g h
        (Segal-arr-in-arr A AisSegal w x y f g) (Segal-arr-in-arr A AisSegal x y z g h))

-- The Segal-associativity-witness curries to define a diagram Δ²×Δ¹ -&gt; A.
-- The Segal-associativity-tetrahedron is extracted via the middle-simplex map \((t, s), r) -&gt; ((t, r), s) from Δ³ to Δ²×Δ¹
#def Segal-associativity-tetrahedron 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : Δ³ -&gt; A
  := \((t, s), r) -&gt; (Segal-associativity-witness extext A AisSegal w x y z f g h) (t, r) s

-- the diagonal composite of three arrows extracted from the Segal-associativity-tetrahedron
#def Segal-triple-composite 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : hom A w z 
  := \t -&gt; (Segal-associativity-tetrahedron extext A AisSegal w x y z f g h) ((t, t), t)

#def Segal-left-associativity-witness 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : hom2 A w y z (Segal-comp A AisSegal w x y f g) h (Segal-triple-composite extext A AisSegal w x y z f g h)
  := \(t, s) -&gt; (Segal-associativity-tetrahedron extext A AisSegal w x y z f g h) ((t, t), s)

#def Segal-right-associativity-witness 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : hom2 A w x z f (Segal-comp A AisSegal x y z g h) (Segal-triple-composite extext A AisSegal w x y z f g h)
  := \(t, s) -&gt; (Segal-associativity-tetrahedron extext A AisSegal w x y z f g h) ((t, s), s)

#def Segal-left-associativity 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : (Segal-comp A AisSegal w y z (Segal-comp A AisSegal w x y f g) h) =
  (Segal-triple-composite extext A AisSegal w x y z f g h)
  := Segal-comp-uniqueness 
        A AisSegal w y z (Segal-comp A AisSegal w x y f g) h
        (Segal-triple-composite extext A AisSegal w x y z f g h)
        (Segal-left-associativity-witness extext A AisSegal w x y z f g h)

#def Segal-right-associativity 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : (Segal-comp A AisSegal w x z f (Segal-comp A AisSegal x y z g h)) =
  (Segal-triple-composite extext A AisSegal w x y z f g h)
  := Segal-comp-uniqueness 
        A AisSegal w x z f (Segal-comp A AisSegal x y z g h)
        (Segal-triple-composite extext A AisSegal w x y z f g h)
        (Segal-right-associativity-witness extext A AisSegal w x y z f g h)

#def Segal-associativity 
  (extext : ExtExt)             -- This proof uses extension extensionality, defined in 4-extension-types.md
  (A : U)                       -- A type.
  (AisSegal : isSegal A)        -- A proof that A is Segal.  
  (w x y z : A)                 -- Four points in A.
  (f : hom A w x)               -- An arrow in A from w to x.
  (g : hom A x y)               -- An arrow in A from x to y.
  (h : hom A y z)               -- An arrow in A from y to z.
  : (Segal-comp A AisSegal w y z (Segal-comp A AisSegal w x y f g) h) =
  (Segal-comp A AisSegal w x z f (Segal-comp A AisSegal x y z g h)) 
  := zig-zag-concat (hom A w z) 
      (Segal-comp A AisSegal w y z (Segal-comp A AisSegal w x y f g) h)
      (Segal-triple-composite extext A AisSegal w x y z f g h)
      (Segal-comp A AisSegal w x z f (Segal-comp A AisSegal x y z g h))
      (Segal-left-associativity extext A AisSegal w x y z f g h) 
      (Segal-right-associativity extext A AisSegal w x y z f g h)
</code></pre>
<h2 id="homotopies">Homotopies</h2>
<p>To be done.</p>
<h2 id="anodyne-maps">Anodyne maps</h2>
<p>To be done.</p>
              
            </div>
          </div><footer>
    <div class="rst-footer-buttons" role="navigation" aria-label="Footer Navigation">
        <a href="../4-extension-types/" class="btn btn-neutral float-left" title="Extension Types"><span class="icon icon-circle-arrow-left"></span> Previous</a>
        <a href="../6-2cat-of-segal-types/" class="btn btn-neutral float-right" title="2-Category of Segal Types">Next <span class="icon icon-circle-arrow-right"></span></a>
    </div>

  <hr/>

  <div role="contentinfo">
    <!-- Copyright etc -->
  </div>

  Built with <a href="https://www.mkdocs.org/">MkDocs</a> using a <a href="https://github.com/readthedocs/sphinx_rtd_theme">theme</a> provided by <a href="https://readthedocs.org">Read the Docs</a>.
</footer>
          
        </div>
      </div>

    </section>

  </div>

  <div class="rst-versions" role="note" aria-label="Versions">
  <span class="rst-current-version" data-toggle="rst-current-version">
    
    
      <span><a href="../4-extension-types/" style="color: #fcfcfc">&laquo; Previous</a></span>
    
    
      <span><a href="../6-2cat-of-segal-types/" style="color: #fcfcfc">Next &raquo;</a></span>
    
  </span>
</div>
    <script>var base_url = '../..';</script>
    <script src="../../js/theme_extra.js" defer></script>
    <script src="../../js/theme.js" defer></script>
      <script src="../../search/main.js" defer></script>
    <script defer>
        window.onload = function () {
            SphinxRtdTheme.Navigation.enable(true);
        };
    </script>

</body>
</html>
