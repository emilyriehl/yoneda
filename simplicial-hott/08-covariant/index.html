<!DOCTYPE html>
<html class="writer-html5" lang="en" >
<head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <link rel="shortcut icon" href="../../img/favicon.ico" />
    <title>Covariantly functorial type families - yoneda</title>
    <link rel="stylesheet" href="../../css/theme.css" />
    <link rel="stylesheet" href="../../css/theme_extra.css" />
        <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/10.5.0/styles/github.min.css" />
        <link href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.7.0/styles/default.min.css" rel="stylesheet" />
    <link rel="apple-touch-icon" sizes="180x180" href="/rzk/apple-touch-icon.png">
    <link rel="icon" type="image/png" sizes="32x32" href="/rzk/favicon-32x32.png">
    <link rel="icon" type="image/png" sizes="16x16" href="/rzk/favicon-16x16.png">
    <link rel="manifest" href="/rzk/site.webmanifest">
<!-- <link rel="stylesheet" href="../../css/highlightjs-11.7.0.min.css" /> -->
    <link rel="stylesheet" href="../../css/highlightjs-copy.css" />
    <script src="../../js/highlight-11.7.0.min.js"></script>
    <script src="../../js/rzk.js"></script>
    <link rel="stylesheet" href="../../css/gruvbox-light.css" />
    <!-- <link rel="stylesheet" href="https://unpkg.com/highlightjs-copy/dist/highlightjs-copy.min.css" /> -->
    <script src="../../js/highlightjs-copy.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.0/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
    <script>
      hljs.addPlugin(new CopyButtonPlugin());
      hljs.highlightAll();
    </script>
    
    
      <script>
        // Current page data
        var mkdocs_page_name = "Covariantly functorial type families";
        var mkdocs_page_input_path = "simplicial-hott/08-covariant.md";
        var mkdocs_page_url = null;
      </script>
    
    <script src="../../js/jquery-3.6.0.min.js" defer></script>
    <!--[if lt IE 9]>
      <script src="../../js/html5shiv.min.js"></script>
    <![endif]-->
      <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/10.5.0/highlight.min.js"></script>
      <script>hljs.initHighlightingOnLoad();</script>
 
</head>

<body class="wy-body-for-nav" role="document">

  <div class="wy-grid-for-nav">
    <nav data-toggle="wy-nav-shift" class="wy-nav-side stickynav">
    <div class="wy-side-scroll">
      <div class="wy-side-nav-search">
          <a href="../.." class="icon icon-home"> yoneda
        </a><div role="search">
  <form id ="rtd-search-form" class="wy-form" action="../../search.html" method="get">
      <input type="text" name="q" placeholder="Search docs" title="Type search term here" />
  </form>
</div>
      </div>

      <div class="wy-menu wy-menu-vertical" data-spy="affix" role="navigation" aria-label="Navigation menu">
              <p class="caption"><span class="caption-text">General</span></p>
              <ul>
                  <li class="toctree-l1"><a class="reference internal" href="../..">About</a>
                  </li>
              </ul>
              <p class="caption"><span class="caption-text">HoTT</span></p>
              <ul>
                  <li class="toctree-l1"><a class="" href="../../hott/0-common.md">Common</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/1-paths.md">Paths</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/2-homotopies.md">Homotopies</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/3-equivalences.md">Equivalences</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/4-contractible.md">Contractible</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/5-sigma.md">Sigma types</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/6-fibers.md">Fibers</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../../hott/7-trivial-fibrations.md">Trivial fibrations</a>
                  </li>
              </ul>
              <p class="caption"><span class="caption-text">Simplicial HoTT</span></p>
              <ul>
                  <li class="toctree-l1"><a class="" href="../3-simplicial-type-theory.md">Simplicial Type Theory</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../4-extension-types.md">Extension Types</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../5-segal-types.md">Segal Types</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../6-2cat-of-segal-types.md">2-Category of Segal Types</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../7-discrete.md">Discrete Types</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../8-covariant.md">Covariantly Functorial Type Families</a>
                  </li>
                  <li class="toctree-l1"><a class="" href="../9-yoneda.md">The Yoneda Lemma</a>
                  </li>
              </ul>
      </div>
    </div>
    </nav>

    <section data-toggle="wy-nav-shift" class="wy-nav-content-wrap">
      <nav class="wy-nav-top" role="navigation" aria-label="Mobile navigation menu">
          <i data-toggle="wy-nav-top" class="fa fa-bars"></i>
          <a href="../..">yoneda</a>
        
      </nav>
      <div class="wy-nav-content">
        <div class="rst-content"><div role="navigation" aria-label="breadcrumbs navigation">
  <ul class="wy-breadcrumbs">
    <li><a href="../.." class="icon icon-home" alt="Docs"></a> &raquo;</li>
      <li>Covariantly functorial type families</li>
    <li class="wy-breadcrumbs-aside">
    </li>
  </ul>
  <hr/>
</div>
          <div role="main" class="document" itemscope="itemscope" itemtype="http://schema.org/Article">
            <div class="section" itemprop="articleBody">
              
                <h1 id="covariantly-functorial-type-families">Covariantly functorial type families</h1>
<p>These formalisations correspond to Section 8 of RS17 paper.</p>
<p>This is a literate <code>rzk</code> file:</p>
<pre><code class="language-rzk">#lang rzk-1
</code></pre>
<h2 id="prerequisites">Prerequisites</h2>
<ul>
<li><code>hott/*</code> - We require various prerequisites from homotopy type theory, for instance the notion of contractible types.</li>
<li><code>3-simplicial-type-theory.md</code> — We rely on definitions of simplicies and their subshapes.</li>
<li><code>5-segal-types.md</code> - We make use of the notion of Segal types and their structures.</li>
</ul>
<h2 id="dependent-hom-types">Dependent hom types</h2>
<p>In a type family over a base type, there is a dependent hom type of arrows that live over a specified arrow in the base type.</p>
<pre><code class="language-rzk">-- [RS17, Section 8 Prelim]
-- The type of dependent arrows in C over f from u to v
#def dhom 
    (A : U)                     -- The base type.
    (x y : A)                   -- Two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (C : A -&gt; U)                -- A type family.
    (u : C x)                   -- A lift of the domain.
    (v : C y)                   -- A lift of the codomain.
    : U
    := &lt;{t : 2 | Δ¹ t } -&gt; C (f t) 
            [t === 0_2 |-&gt; u, 
            t === 1_2 |-&gt; v ]&gt;
</code></pre>
<p>It will be convenient to collect together dependent hom types with fixed domain but varying codomain.</p>
<pre><code class="language-rzk">#def dhomFrom
    (A : U)                     -- The base type.
    (x y : A)                   -- Two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (C : A -&gt; U)                -- A type family.
    (u : C x)                   -- A lift of the domain.
    : U
    := (∑ (v : C y), dhom A x y f C u v)
</code></pre>
<p>There is also a type of dependent commutative triangles over a base commutative triangle.</p>
<pre><code class="language-rzk">#def dhom2 
    (A : U)                         -- The base type.
    (x y z : A)                     -- Three points in the base.
    (f : hom A x y)                 -- An arrow in the base.
    (g : hom A y z)                 -- An arrow in the base.
    (h : hom A x z)                 -- An arrow in the base.
    (alpha : hom2 A x y z f g h)    -- A composition witness in the base.
    (C : A -&gt; U)                    -- A type family.
    (u : C x)                       -- A lift of the initial point.
    (v : C y)                       -- A lift of the second point.
    (w : C z)                       -- A lift of the third point.
    (ff : dhom A x y f C u v)       -- A lift of the first arrow.
    (gg : dhom A y z g C v w)       -- A lift of the second arrow.
    (hh : dhom A x z h C u w)       -- A lift of the diagonal arrow.
    : U
    := &lt;{(t1, t2) : 2 * 2 | Δ² (t1, t2)} -&gt; C (alpha (t1, t2)) 
            [t2 === 0_2 |-&gt; ff t1, 
            t1 === 1_2 |-&gt; gg t2, 
            t2 === t1 |-&gt; hh t2 ]&gt;
</code></pre>
<h2 id="covariant-families">Covariant families</h2>
<p>A family of types over a base type is covariant if every arrow in the base has a unique lift with specified domain.</p>
<pre><code class="language-rzk">-- [RS17, Definition 8.2]
#def isCovFam 
    (A : U)
    (C : A -&gt; U)
    : U
    := (x : A) -&gt; (y : A) -&gt; (f : hom A x y) -&gt; (u : C x) 
        -&gt; isContr (dhomFrom A x y f C u)

-- Type of covariant families over a fixed type
#def covFam (A : U) : U
    := (∑ (C :  ((a : A) -&gt; U)), isCovFam A C)
</code></pre>
<h2 id="representable-covariant-families">Representable covariant families</h2>
<p>If A is a Segal type and a : A is any term, then hom A a defines a covariant family over A, and conversely if this family is covariant for every a : A, then A must be a Segal type. The proof involves a rather lengthy composition of equivalences.</p>
<pre><code class="language-rzk">#def representable-dhom
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    (v : hom A a y)             -- A lift of the codomain.
    : U
    := dhom A x y f (\z -&gt; hom A a z) u v

-- By uncurrying (RS 4.2) we have an equivalence:
#def representable-dhom-uncurry
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    (v : hom A a y)             -- A lift of the codomain.
    : Eq (representable-dhom A a x y f u v) 
    (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;)
    := curry-uncurry 2 2 Δ¹ ∂Δ¹ Δ¹ ∂Δ¹ (\t s -&gt; A) 
        (\(t, s) -&gt; recOR(((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ))

#def representable-dhomFrom
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : U
    := dhomFrom A x y f (\z -&gt; hom A a z) u                     

-- By uncurrying (RS 4.2) we have an equivalence:
#def representable-dhomFrom-uncurry
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (representable-dhomFrom A a x y f u)
        (∑ (v : hom A a y), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
    := family-Eq-total-Eq (hom A a y) (\v -&gt; representable-dhom A a x y f u v)
        (\v -&gt; (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
        (\v -&gt; representable-dhom-uncurry A a x y f u v)

#def square-to-hom2-pushout
    (A : U)
    (w x y z : A)
    (u : hom A w x)
    (f : hom A x z)
    (g : hom A w y)
    (v : hom A y z)
    : (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; g t, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;)
    -&gt; (∑ (d : hom A w z), prod (hom2 A w x z u f d) (hom2 A w y z g v d))
    := \sq -&gt; ((\t -&gt; sq (t, t)), (\(t, s) -&gt; sq (s, t), \(t, s) -&gt; sq (t, s)))

#def hom2-pushout-to-square
    (A : U)
    (w x y z : A)
    (u : hom A w x)
    (f : hom A x z)
    (g : hom A w y)
    (v : hom A y z)
    : (∑ (d : hom A w z), prod (hom2 A w x z u f d) (hom2 A w y z g v d))
    -&gt; (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; g t, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;)
    := \(d, (alpha1, alpha2)) (t, s) -&gt; recOR (t &lt;= s |-&gt; alpha1 (s, t),
                                                s &lt;= t |-&gt; alpha2 (t, s))
#def square-hom2-pushout-Eq
    (A : U)
    (w x y z : A)
    (u : hom A w x)
    (f : hom A x z)
    (g : hom A w y)
    (v : hom A y z)
    : Eq (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; g t, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;)
        (∑ (d : hom A w z), prod (hom2 A w x z u f d) (hom2 A w y z g v d))
    := (square-to-hom2-pushout A w x y z u f g v, 
        ((hom2-pushout-to-square A w x y z u f g v, \sq -&gt; refl),
        (hom2-pushout-to-square A w x y z u f g v, \alphas -&gt; refl)))

#def representable-dhomFrom-uncurry-hom2
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (∑ (v : hom A a y), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
        (∑ (v : hom A a y), (∑ (d : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
    := family-Eq-total-Eq (hom A a y) 
        (\v -&gt; (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
        (\v -&gt; (∑ (d : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
        (\v -&gt; square-hom2-pushout-Eq A a x a y u f (id-arr A a) v)

#def representable-dhomFrom-hom2
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (∑ (v : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
    := triple_compose_Eq
        (representable-dhomFrom A a x y f u)
        (∑ (v : hom A a y), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
        (∑ (v : hom A a y), (∑ (d : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
        (∑ (d : hom A a y), (∑ (v : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
        (representable-dhomFrom-uncurry A a x y f u)
        (representable-dhomFrom-uncurry-hom2 A a x y f u)
        (sigma-fubini (hom A a y) (hom A a y) 
            (\v d -&gt; prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))

#def representable-dhomFrom-hom2-dist
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d)))
    := RightCancel_Eq
        (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d)))
        (∑ (d : hom A a y), (∑ (v : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
        (representable-dhomFrom-hom2 A a x y f u)
        (family-Eq-total-Eq (hom A a y)
            (\d -&gt; (prod (hom2 A a x y u f d) (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d)))
            (\d -&gt; (∑ (v : hom A a y), prod (hom2 A a x y u f d) (hom2 A a a y (id-arr A a) v d)))
            (\d -&gt; (prod-distribute-sigma (hom2 A a x y u f d) (hom A a y)(\v -&gt; hom2 A a a y (id-arr A a) v d))))
</code></pre>
<p>Now we introduce the hypothesis that A is Segal type.</p>
<pre><code class="language-rzk">#def Segal-representable-dhomFrom-path-space
    (A : U)                     -- The ambient type.
    (AisSegal : isSegal A)      -- A proof that A is a Segal type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d))))
    := RightCancel_Eq
        (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d))))
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d)))
        (representable-dhomFrom-hom2-dist A a x y f u)
        (family-Eq-total-Eq (hom A a y)
            (\d -&gt; (prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d))))
            (\d -&gt; (prod (hom2 A a x y u f d) (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d)))
            (\d -&gt; (family-Eq-total-Eq (hom2 A a x y u f d) 
                (\alpha -&gt; (∑ (v : hom A a y), (v = d)))
                (\alpha -&gt; (∑ (v : hom A a y), hom2 A a a y (id-arr A a) v d))
                (\alpha -&gt; (family-Eq-total-Eq (hom A a y)
                    (\v -&gt; (v = d))
                    (\v -&gt; hom2 A a a y (id-arr A a) v d)
                    (\v -&gt; (Eq-Segal-homotopy-hom2 A AisSegal a y v d)))))))


#def codomain-based-paths-contraction
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    (d : hom A a y)
    : Eq (prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d))) (hom2 A a x y u f d) 
    := contractible-fibers-projection-Eq (hom2 A a x y u f d) (\alpha -&gt; (∑ (v : hom A a y), (v = d)))
        (\alpha -&gt; codomain-based-paths-contractible (hom A a y) d)

#def isSegal-representable-dhomFrom-hom2
    (A : U)                     -- The ambient type.
    (AisSegal : isSegal A)      -- A proof that A is a Segal type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (hom2 A a x y u f d))
    := compose_Eq 
        (representable-dhomFrom A a x y f u)
        (∑ (d : hom A a y), (prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d))))
        (∑ (d : hom A a y), (hom2 A a x y u f d))
        (Segal-representable-dhomFrom-path-space A AisSegal a x y f u)
        (family-Eq-total-Eq (hom A a y)
            (\d -&gt; prod (hom2 A a x y u f d) (∑ (v : hom A a y), (v = d)))
            (\d -&gt; hom2 A a x y u f d)
            (\d -&gt; codomain-based-paths-contraction A a x y f u d))

#def isSegal-representable-dhomFrom-contractible
    (A : U)                     -- The ambient type.
    (AisSegal : isSegal A)      -- A proof that A is a Segal type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : isContr (representable-dhomFrom A a x y f u)
    := isEquiv-toContr-isContr (representable-dhomFrom A a x y f u)
                (∑ (d : hom A a y), (hom2 A a x y u f d))
                (isSegal-representable-dhomFrom-hom2 A AisSegal a x y f u)
                (AisSegal a x y u f)
</code></pre>
<p>Finally, we see that covariantly hom families in a Segal type are covariant. </p>
<pre><code class="language-rzk">-- [RS, Proposition 8.13(&lt;-)]
#def isSegal-representable-isCovFam 
    (A : U)
    (AisSegal : isSegal A)
    (a : A)
    : isCovFam A (\x -&gt; hom A a x)
    := \x y f u -&gt; isSegal-representable-dhomFrom-contractible A AisSegal a x y f u 
</code></pre>
<p>The proof of the claimed converse result given in the original source is circular - using Proposition 5.10, which holds only for Segal types - so instead we argue as follows:</p>
<pre><code class="language-rzk">-- [RS, Proposition 8.13(-&gt;)]
#def representable-isCovFam-isSegal
    (A : U)
    (repiscovfam : (a : A) -&gt; isCovFam A (\x -&gt; hom A a x))
    : isSegal A
    := \x y z f g -&gt; first-isContr-sigma
        (∑ (h : hom A x z), hom2 A x y z f g h)
        (\hk -&gt; ∑ (v : hom A x z), hom2 A x x z (id-arr A x) v (first hk))
        (\hk -&gt; (first hk, \(t, s) -&gt; first hk s))
        (isEquiv-toContr-isContr
            (∑ (hk : ∑ (h : hom A x z), hom2 A x y z f g h), ∑ (v : hom A x z), hom2 A x x z (id-arr A x) v (first hk))
            (representable-dhomFrom A x y z g f)
            (sym_Eq (representable-dhomFrom A x y z g f)
                (∑ (hk : ∑ (h : hom A x z), hom2 A x y z f g h), ∑ (v : hom A x z), hom2 A x x z (id-arr A x) v (first hk))
                (compose_Eq
                    (representable-dhomFrom A x y z g f)
                    (∑ (h : hom A x z), (prod (hom2 A x y z f g h) (∑ (v : hom A x z), hom2 A x x z (id-arr A x) v h)))
                    (∑ (hk : ∑ (h : hom A x z), hom2 A x y z f g h), ∑ (v : hom A x z), hom2 A x x z (id-arr A x) v (first hk))
                    (representable-dhomFrom-hom2-dist A x y z g f)
                    (assoc
                        (hom A x z)
                        (\h -&gt; hom2 A x y z f g h)
                        (\h _ -&gt; ∑ (v : hom A x z), hom2 A x x z (id-arr A x) v h))))
            (repiscovfam x y z g f))
</code></pre>
<p>While not needed to prove Proposition 8.13, it is interesting to observe that the dependent hom types in a representable family can be understood as extension types as follows.</p>
<pre><code class="language-rzk">#def cofibration-union-test
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
            [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;
       &lt;{(t, s) : 2 * 2 | ((t === 1_2) /\ (Δ¹ s))} -&gt; A [ ((t === 1_2) /\ (s === 0_2)) |-&gt; a,  ((t === 1_2) /\ (s === 1_2)) |-&gt; y ]&gt;
    := cofibration_union (2 * 2) 
    (\(t, s) -&gt; (t === 1_2) /\ Δ¹ s)
    (\(t, s) -&gt; ((t === 0_2) /\ Δ¹ s) \/ (Δ¹ t /\ (s === 0_2)) \/ (Δ¹ t /\ (s === 1_2)))
    (\(t, s) -&gt; A)
    (\(t, s) -&gt; recOR(((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ))  

#def base-hom-rewriting
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (&lt;{(t, s) : 2 * 2 | ((t === 1_2) /\ (Δ¹ s))} -&gt; A [ ((t === 1_2) /\ (s === 0_2)) |-&gt; a,  ((t === 1_2) /\ (s === 1_2)) |-&gt; y ]&gt;)        
        (hom A a y)
    := (\v -&gt; (\r -&gt; v ((1_2, r))), 
            ((\v -&gt; \(t, s) -&gt; v s, 
                \v -&gt; refl),
            (\v -&gt; \(t, s) -&gt; v s, 
                \v -&gt; refl)))       

#def base-hom-expansion
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
            [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;        
        (hom A a y) 
    := compose_Eq 
        (&lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
            [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt; )
        (&lt;{(t, s) : 2 * 2 | ((t === 1_2) /\ (Δ¹ s))} -&gt; A [ ((t === 1_2) /\ (s === 0_2)) |-&gt; a,  ((t === 1_2) /\ (s === 1_2)) |-&gt; y ]&gt;)         
        (hom A a y)
        (cofibration-union-test A a x y f u)
        (base-hom-rewriting A a x y f u)

#def representable-dhomFrom-expansion
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.
    : Eq (∑ (sq : &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
                        [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; (sq (1_2, s)), 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
        (∑ (v : hom A a y), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
    := pullback-is-equiv-total-eq ( &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
            [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt; )
                        (hom A a y)
                        (first (base-hom-expansion A a x y f u))
                        (second (base-hom-expansion A a x y f u))
            (\v -&gt; (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))

#def representable-dhomFrom-composite-expansion
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.    
    : Eq (representable-dhomFrom A a x y f u) 
            (∑ (sq : &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
                        [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; (sq (1_2, s)), 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
    := RightCancel_Eq (representable-dhomFrom A a x y f u) 
                (∑ (sq : &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
                        [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; (sq (1_2, s)), 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))

        (∑ (v : hom A a y), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; v s, 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
             (representable-dhomFrom-uncurry A a x y f u)
             (representable-dhomFrom-expansion A a x y f u)

#def representable-dhomFrom-cofibration-composition
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.    
    : Eq (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t]&gt; )
        (∑ (sq : &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
                        [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; (sq (1_2, s)), 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
    := cofibration-composition (2 * 2) Δ¹×Δ¹ ∂□ 
            (\(t, s) -&gt; ((t === 0_2) /\ Δ¹ s) \/ (Δ¹ t /\ (s === 0_2)) \/ (Δ¹ t /\ (s === 1_2)))
            (\ts -&gt; A) 
            (\(t, s) -&gt; recOR( ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t))

#def representable-dhomFrom-as-extension-type
    (A : U)                     -- The ambient type.
    (a x y : A)                 -- The representing object and two points in the base.
    (f : hom A x y)             -- An arrow in the base.
    (u : hom A a x)             -- A lift of the domain.    
    : Eq (representable-dhomFrom A a x y f u) 
        (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                                (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                                (Δ¹ t /\ (s === 1_2)) |-&gt; f t]&gt; )
    := RightCancel_Eq (representable-dhomFrom A a x y f u) 
                (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                                (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                                (Δ¹ t /\ (s === 1_2)) |-&gt; f t]&gt; )
                (∑ (sq : &lt;{(t, s) : 2 * 2 | ∂□ (t, s)} -&gt; A 
                        [ ((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                        (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                        (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;), (&lt;{(t, s) : 2 * 2 | Δ¹×Δ¹ (t, s)} -&gt; A [((t === 0_2) /\ Δ¹ s) |-&gt; u s, 
                                            ((t === 1_2) /\ Δ¹ s) |-&gt; (sq (1_2, s)), 
                                            (Δ¹ t /\ (s === 0_2)) |-&gt; a, 
                                            (Δ¹ t /\ (s === 1_2)) |-&gt; f t ]&gt;))
                (representable-dhomFrom-composite-expansion A a x y f u)
                (representable-dhomFrom-cofibration-composition A a x y f u)
</code></pre>
<h2 id="covariant-lifts-transport-and-uniqueness">Covariant lifts, transport, and uniqueness</h2>
<p>In a covariant family C over a base type A, a term u : C x may be transported along an arrow f : hom A x y to give a term in C y.</p>
<pre><code class="language-rzk">-- [RS17, covariant transport from beginning of Section 8.2]
#def covTrans
    (A : U)
    (x y : A)
    (f : hom A x y)
    (C : A -&gt; U)
    (CisCov : isCovFam A C)
    (u : C x)
    : C y
    := first (contraction-center (dhomFrom A x y f C u) (CisCov x y f u))

-- [RS17, covariant lift from beginning of Section 8.2]
#def covLift 
    (A : U)
    (x y : A)
    (f : hom A x y)
    (C : A -&gt; U)
    (CisCov : isCovFam A C)
    (u : C x)
    : (dhom A x y f C u (covTrans A x y f C CisCov u))
    := second (contraction-center (dhomFrom A x y f C u) (CisCov x y f u))

#def covUniqueness
    (A : U)
    (x y : A)
    (f : hom A x y)
    (C : A -&gt; U)
    (CisCov : isCovFam A C)
    (u : C x)
    (lift : dhomFrom A x y f C u)
    : (covTrans A x y f C CisCov u) = (first lift)
    := total-path-to-base-path
        (C y)
        (\v -&gt; dhom A x y f C u v)
        (contraction-center (dhomFrom A x y f C u) (CisCov x y f u))
        lift
        (contracting-htpy (dhomFrom A x y f C u) (CisCov x y f u) lift)
</code></pre>
<h2 id="covariant-functoriality">Covariant functoriality</h2>
<p>The covariant transport operation defines a covariantly functorial action of arrows in the base on terms in the fibers. In particular, there is an identity transport law.</p>
<pre><code class="language-rzk">#def d-id-arr
    (A : U)
    (x : A)
    (C : A -&gt; U)
    (u : C x)
    : dhom A x x (id-arr A x) C u u
    := \t -&gt; u

-- [RS17, Proposition 8.16, Part 2]
-- Covariant families preserve identities
#def covPresId
    (A : U)
    (x : A)
    (C : A -&gt; U)
    (CisCov : isCovFam A C)
    (u : C x)
    : (covTrans A x x (id-arr A x) C CisCov u) = u
    := covUniqueness A x x (id-arr A x) C CisCov u (u, d-id-arr A x C u)
</code></pre>
<h2 id="natural-transformations">Natural transformations</h2>
<p>A fiberwise map between covariant families is automatically "natural" commuting with the covariant lifts.</p>
<pre><code class="language-rzk">-- [RS17, Proposition 8.17]
-- Covariant naturality
#def covariant-transformation-application
    (A : U)
    (x y : A)
    (f : hom A x y)
    (C D : A -&gt; U)
    (CisCov : isCovFam A C)
    (DisCov : isCovFam A D)
    (phi : (z : A) -&gt; C z -&gt; D z)
    (u : C x)
    : dhomFrom A x y f D (phi x u)
    := (phi y (covTrans A x y f C CisCov u), 
    \t -&gt; phi (f t) (covLift A x y f C CisCov u t))

#def covariant-transformation-naturality
    (A : U)
    (x y : A)
    (f : hom A x y)
    (C D : A -&gt; U)
    (CisCov : isCovFam A C)
    (DisCov : isCovFam A D)
    (phi : (z : A) -&gt; C z -&gt; D z)
    (u : C x)
    : (covTrans A x y f D DisCov (phi x u)) = (phi y (covTrans A x y f C CisCov u))
    := covUniqueness A x y f D DisCov (phi x u)
        (covariant-transformation-application A x y f C D CisCov DisCov phi u)
</code></pre>
              
            </div>
          </div><footer>

  <hr/>

  <div role="contentinfo">
    <!-- Copyright etc -->
  </div>

  Built with <a href="https://www.mkdocs.org/">MkDocs</a> using a <a href="https://github.com/readthedocs/sphinx_rtd_theme">theme</a> provided by <a href="https://readthedocs.org">Read the Docs</a>.
</footer>
          
        </div>
      </div>

    </section>

  </div>

  <div class="rst-versions" role="note" aria-label="Versions">
  <span class="rst-current-version" data-toggle="rst-current-version">
    
    
    
  </span>
</div>
    <script>var base_url = '../..';</script>
    <script src="../../js/theme_extra.js" defer></script>
    <script src="../../js/theme.js" defer></script>
      <script src="../../search/main.js" defer></script>
    <script defer>
        window.onload = function () {
            SphinxRtdTheme.Navigation.enable(true);
        };
    </script>

</body>
</html>
